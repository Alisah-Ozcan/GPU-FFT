file(GLOB_RECURSE LIB_SOURCE_FILES
    RELATIVE ${CMAKE_CURRENT_SOURCE_DIR}
    CONFIGURE_DEPENDS
    lib/*.cu
)

file(GLOB_RECURSE LIB_HEADER_FILES
    RELATIVE ${CMAKE_CURRENT_SOURCE_DIR}
    CONFIGURE_DEPENDS
    include/*.cuh
)

add_library(fft STATIC ${LIB_SOURCE_FILES})

set_target_properties(fft PROPERTIES
  VERSION ${PROJECT_VERSION}
  SOVERSION 1
  OUTPUT_NAME "fft-${PROJECT_VERSION}"
)

if(NOT DEFINED COPLEX_DATA_TYPE)
    set(COPLEX_DATA_TYPE 0)
endif()

target_include_directories(
    fft
    PUBLIC
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}/include>
    $<INSTALL_INTERFACE:include/GPUFFT-${PROJECT_VERSION}>
)

if(COPLEX_DATA_TYPE EQUAL 0)
    add_compile_definitions(FLOAT_64)
    target_compile_definitions(fft PRIVATE FLOAT_64)
elseif(COPLEX_DATA_TYPE EQUAL 1)
    add_compile_definitions(FLOAT_32)
    target_compile_definitions(fft PRIVATE FLOAT_32)
else()
    message(FATAL_ERROR "Please use valid COPLEX_DATA_TYPE! (0,1)")
endif()

set_target_properties(fft PROPERTIES
    CUDA_SEPARABLE_COMPILATION ON
    POSITION_INDEPENDENT_CODE ON
    CMAKE_CUDA_RESOLVE_DEVICE_SYMBOLS ON
    CUDA_RUNTIME_LIBRARY Static
    CUDA_ARCHITECTURES ${CMAKE_CUDA_ARCHITECTURES}
)

install(TARGETS fft
  EXPORT ${GPUFFT_TARGETS_EXPORT_NAME}
  RUNTIME DESTINATION ${RUNTIME_DESTINATION}
  LIBRARY DESTINATION ${LIBRARY_DESTINATION}
  ARCHIVE DESTINATION ${ARCHIVE_DESTINATION}
)

install(FILES ${LIB_HEADER_FILES} DESTINATION ${INCLUDES_INSTALL_DIR})

